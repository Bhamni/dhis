allprojects {
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: 'maven'

    version = "1.0-SNAPSHOT"
    group = "org.openmrs.module"

    configurations {
        includedJars
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://mavenrepo.openmrs.org/nexus/content/repositories/public" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "http://maven.springframework.org/release" }
        maven { url "https://clojars.org/repo" }
    }

    dependencies {
        compile group: "org.ict4h", name: "aggregate-query-service", version: "1.1-SNAPSHOT"
        includedJars group: "org.ict4h", name: "aggregate-query-service", version: "1.1-SNAPSHOT"
        includedJars group: "org.clojure", name: "clojure", version: "1.6.0"

        compile group: "org.openmrs.api", name: "openmrs-api", version: "1.10.1"
        compile group: "org.openmrs.module", name: "webservices.rest-omod-common", version: "2.6"
        compile group: "org.springframework", name: "spring-web", version: "3.0.5.RELEASE"
        testCompile group: "junit", name: "junit", version: "4.12"
    }

    subprojects {
        apply plugin: "java"
    }

}
apply plugin: 'maven-publish'
apply plugin: 'maven-publish-auth'
buildscript {
    dependencies {
        classpath group: "org.hibernate.build.gradle", name: "gradle-maven-publish-auth", version: "2.0.1"
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repository.jboss.org/nexus/content/groups/public/" }
    }
}

task createOmod(type: Jar, dependsOn: subprojects.tasks['build']) {
    baseName = 'openmrs-module-' + project.name
    extension = 'omod'

    subprojects.each { subproject ->
        from {
            subproject.configurations.archives.allArtifacts.files.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
    }
    into('lib') {
        from configurations.includedJars
    }

    metaInf {
        into('maven/org.openmrs.module/' + project.name + '-omod')
        def pomProperties = new File(getTemporaryDir(), 'pom.properties')
        pomProperties.write "version=${project.version}\n"
        pomProperties << "groupId=${project.group}\n"
        pomProperties << "artifactId=${project.name}\n"
        from pomProperties
    }
}

publishing {
    publications {
        bahmni(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name "bahmni-artifactory"
            url "http://bahmnirepo.thoughtworks.com/artifactory/libs-snapshot-local/"
        }
    }
}

artifacts {
    archives createOmod
}

defaultTasks 'createOmod'